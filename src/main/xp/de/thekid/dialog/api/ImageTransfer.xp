package de.thekid.dialog.api;

import de.thekid.dialog.AlbumImage;

import io.streams.InputStream;
import io.streams.MemoryInputStream;

class ImageTransfer {
  public string $name;
  protected InputStream $bytes;

  /**
   * Set bytes
   */
  public void setBytes(string? $bytes) {
    $this.bytes= new MemoryInputStream($bytes);
  }

  /**
   * Get bytes
   */
  public InputStream getBytes() {
    return $this.bytes;
  }

  /**
   * Convert the given image to an album image
   */
  public AlbumImage asAlbumImage() {

    // Extract meta information
    with ($img= new AlbumImage($this.name)) {
      $info= new img.io.MetaDataReader().read($this.bytes, $this.name);

      // Copy width & height
      with ($dim= $info.imageDimensions()) {
        $img.setWidth($dim[0]);
        $img.setHeight($dim[1]);
      }

      // Extract EXIF and IPTC
      $img.setExifData($info.exifData());
      $img.setIptcData($info.iptcData());

      // Reset input stream
      $this.bytes.seek(0, SEEK_SET);
    }

    return $img;
  }

  /**
   * Creates a string representation
   */
  public string toString() {
    return $this.getClassName() ~ '(name=' ~ $this.name ~ ', #bytes= ' ~ $this.bytes.available() ~ ')';
  }
}