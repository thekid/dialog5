package de.thekid.dialog;

import native standard.sprintf;
import native standard.str_replace;

/**
 * Represents a single chapter within an album.
 *
 * @test     xp://de.thekid.dialog.unittest.AlbumChapterTest
 * @see      xp://de.thekid.dialog.Album
 */
public class AlbumChapter {
  public string $name         = '';
  public AlbumImage[] $images = [];

  /**
   * Constructor
   */
  public __construct($this.name= null) { }

  /**
   * Name accessors
   */
  public void setName($this.name) { }
  public self withName($this.name) -> $this;
  public string getName() -> $this.name;

  /**
   * Add an element to images
   *
   */
  public AlbumImage addImage(AlbumImage $image) {
    $this.images[]= $image;
    return $image;
  }

  /**
   * Add an element to images and return this chapter
   *
   */
  public self withImage(AlbumImage $image) {
    $this.images[]= $image;
    return $this;
  }

  /**
   * Get one image element by position. Returns NULL if the element 
   * can not be found.
   *
   */
  public AlbumImage imageAt(int $i) {
    if (!isset($this.images[$i])) return null;
    return $this.images[$i];
  }

  /**
   * Get number of images
   *
   */
  public int numImages() {
    return $this.images.length;
  }

  /**
   * Retrieve a string representation
   *
   */
  public string toString() {
    $is= '';
    for ($i= 0, $s= $this.images.length; $i < $s; $i++) {
      $is ~= '    ' ~ str_replace("\n", "\n  ", $this.images[$i].toString()) ~ "\n";
    }
    return sprintf(
      "%s(%s) {\n%s  }",
      $this.getClassName(),
      $this.name,
      $is
    );
  }
}
