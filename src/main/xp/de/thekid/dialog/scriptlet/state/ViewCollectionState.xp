package de.thekid.dialog.scriptlet.state;

import scriptlet.xml.XMLScriptletRequest;
import scriptlet.xml.XMLScriptletResponse;
import scriptlet.xml.workflow.Context;
import xml.Node;
import xml.PCData;

import de.thekid.dialog.Album;
import de.thekid.dialog.EntryCollection;
import de.thekid.dialog.scriptlet.NodeHandler;

/**
 * Handles /xml/collection/view
 *
 */
public class ViewCollectionState extends de.thekid.dialog.scriptlet.AbstractDialogState {
  protected static [:NodeHandler] $nodeHandlers = [:];

  static {
    self::$nodeHandlers['de.thekid.dialog.Album']= new NodeHandler() {
      public xml.Node nodeFor(de.thekid.dialog.IEntry? $entry, [:NodeHandler]? $nodeHandlers) {
        with ($album= $entry as Album?) {
          $child= new Node('entry', null, [
            name         : $album.getName(),
            title        : $album.getTitle(),
            num_images   : $album.numImages(),
            num_chapters : $album.numChapters()
          ]);
          $child.addChild(new Node('description', new PCData($album.getDescription())));
          $child.addChild(Node::fromObject($album.getCreatedAt(), 'created'));
          $child.addChild(Node::fromArray($album.highlights, 'highlights'));

          return $child;
        }
      }
    };
  }

  /**
   * Process this state.
   *
   */
  public void process(XMLScriptletRequest? $request, XMLScriptletResponse? $response, Context? $context) {
    $name= $request.getURL().getValue('collection');

    with ($collection= $this._getEntryFor($name, EntryCollection::class) as EntryCollection?) {

      // Add formresult information about collection
      $child= $response.addFormResult(new Node('collection', null, [
        name  : $collection.getName(),
        title : $collection.getTitle(),
        page  : $this.getDisplayPageFor($name)
      ]));
      $child.addChild(new Node('description', new PCData($collection.getDescription())));
      $child.addChild(Node::fromObject($collection.createdAt, 'created'));

      // Add entries from collection 
      $node= $response.addFormResult(new Node('entries'));
      foreach ($entry in $collection.entries) {
        if (!isset(self::$nodeHandlers[$entry.getClassName()])) {
          throw new FormatException('Index contains unknown element "' ~ $entry.getClassName() ~ '"');
        }

        $child= $node.addChild(self::$nodeHandlers[$entry.getClassName()].nodeFor($entry, self::$nodeHandlers));
        $child.setAttribute('type', $entry.getClassName());
      }
    }
  }
}
