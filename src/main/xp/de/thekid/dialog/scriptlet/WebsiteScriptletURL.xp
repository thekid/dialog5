/* This class is part of the XP framework
 *
 * $Id$
 */
 
import scriptlet.xml.XMLScriptletURL;

import native standard.sscanf;
import native pcre.preg_match;

/**
 * Represents the XML scriptlet URLs
 *
 * @see      xp://scriptlet.xml.XMLScriptlet
 * @purpose  URL representation class
 */
class WebsiteScriptletURL extends XMLScriptletURL {
    
  /**
   * Extract information from URL
   *
   */
  protected void extract() {
    if (1 == sscanf($this.getPath(), '/page/%d', $page)) {
      $this.setStateName('static');
      $this.setValue('index', $page);
      return;
    }
    
    if (preg_match('#/album/([^/]+(/[^/]+)?)/([0-9]+)/([hi]),([0-9]+)$#', $this.getPath(), $parts)) {
      $this.setStateName('image/view');
      $this.setValue('album', $parts[1]);
      $this.setValue('chapter', $parts[3]);
      $this.setValue('type', $parts[4]);
      $this.setValue('index', $parts[5]);
      return;
    }
    
    if (preg_match('#/album/([^/]+(/[^/]+)?)/([0-9]+)$#', $this.getPath(), $parts)) {
      $this.setStateName('chapter/view');
      $this.setValue('album', $parts[1]);
      $this.setValue('chapter', $parts[3]);
      return;
    }
    
    if (preg_match('#/album/([^/]+(/[^/]+)?)$#', $this.getPath(), $parts)) {
      $this.setStateName('album/view');
      $this.setValue('album', $parts[1]);
      return;
    }
    
    if (1 == sscanf($this.getPath(), '/collection/%[^/]', $collection)) {
      $this.setStateName('collection/view');
      $this.setValue('collection', $collection);
      return;
    }
    
    if (2 == sscanf($this.getPath(), '/shot/%[^/]/%d', $shot, $mode)) {
      $this.setStateName('shot/view');
      $this.setValue('shot', $shot);
      $this.setValue('mode', $mode);
      return;
    }
    
    parent::extract();
  }
  
  /**
   * Set value
   *
   */
  protected void setValue(string $key, var $value) {
    $this.values.put('dialog.' ~ $key, $value);
  }

  /**
   * Retrieve stored value
   *
   */
  public var getValue(string $key) {
    return $this.values.get('dialog.' ~ $key);
  }
}
